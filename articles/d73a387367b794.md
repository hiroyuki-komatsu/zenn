---
title: "Bazel ã‹ã‚‰ pkg-config ã‚’ä½¿ã†"
emoji: "ğŸ”¨"
type: "tech"
topics:
  - "bazel"
published: true
published_at: "2022-07-19 01:15"
---

åˆç¨¿: 2022-07-18
å°æ¾å¼˜å¹¸ ([@komatsuh](https://twitter.com/komatsuh))

# è¨˜äº‹ã®å†…å®¹
ãƒ“ãƒ«ãƒ‰ã‚·ã‚¹ãƒ†ãƒ ã® Bazel ã§ã€pkg-config ã‚³ãƒãƒ³ãƒ‰ã‹ã‚‰å–å¾—ã§ãã‚‹ãƒ“ãƒ«ãƒ‰ãƒ•ãƒ©ã‚°ç­‰ã‚’ä½¿ãˆã‚‹ã‚ˆã†ã«ã™ã‚‹æ–¹æ³•ã‚’ç´¹ä»‹ã—ã¾ã™ã€‚

# ç”¨èª

* Bazel: ãƒ“ãƒ«ãƒ‰ã‚·ã‚¹ãƒ†ãƒ ã€‚Make ã‚„ CMake ãªã©ã¨åŒã˜å½¹å‰²ã®ã‚‚ã®ã€‚ã“ã®è¨˜äº‹ã¯ Bazel ã«ã¤ã„ã¦ã¯ã™ã§ã«çŸ¥ã£ã¦ã„ã‚‹æ–¹ã‚€ã‘ã§ã™ã€‚
* pkg-config: ãƒ“ãƒ«ãƒ‰ãƒ•ãƒ©ã‚°ã‚’å–å¾—ã™ã‚‹ãŸã‚ã®ã‚³ãƒãƒ³ãƒ‰ãƒ©ã‚¤ãƒ³ã€‚C++ ã®ãƒ˜ãƒƒãƒ€ãƒ¼ãƒ•ã‚¡ã‚¤ãƒ«ç”¨ã‚¤ãƒ³ã‚¯ãƒ«ãƒ¼ãƒ‰ãƒ‘ã‚¹ã‚„ãƒªãƒ³ã‚¯ã‚ªãƒ—ã‚·ãƒ§ãƒ³ç­‰ã€é–‹ç™ºç’°å¢ƒã«ä¾å­˜ã™ã‚‹æƒ…å ±ã‚’å–å¾—ã§ãã¾ã™ã€‚

# æ–¹æ³•

* `WORKSPACE.bazel` ã« pkg-config çµŒç”±ã§ä½¿ç”¨ã—ãŸã„ãƒ‘ãƒƒã‚±ãƒ¼ã‚¸ã®è¨­å®šã‚’è¨˜è¿°ã™ã‚‹
* `BUILD.bazel` ã®ãƒ«ãƒ¼ãƒ«ã«è¨­å®šã—ãŸãƒ‘ãƒƒã‚±ãƒ¼ã‚¸ã‚’è¿½åŠ ã™ã‚‹
* ä¸‹è¨˜ã® `pkg_config_repository.bzl` (æŠ˜ã‚ŠãŸãŸã¾ã‚Œã¦ã„ã¾ã™) ã‚’ãƒ•ã‚¡ã‚¤ãƒ«ã¨ã—ã¦ä¿å­˜ã™ã‚‹

```py:WORKSPACE.bazel
load("//:pkg_config_repository.bzl", "pkg_config_repository")

pkg_config_repository(
  name = "glib",
  packages = ["glib2.0", "gobject-2.0"],
)
```

```py:BUILD.bazel
cc_library(
  name = "glib_library",
  srcs = ["glib_library.cc"],
  â€¦
  deps = ["@glib//:glib"],
) 
```

:::details pkg_config_repository.bzl
```py:pkg_config_repository.bzl

BUILD_TEMPLATE = """
package(
    default_visibility = ["//visibility:public"],
)
cc_library(
    name = "{name}",
    hdrs = glob([
        {hdrs}
    ]),
    copts = [
        {copts}
    ],
    includes = [
        {includes}
    ],
    linkopts = [
        {linkopts}
    ],
)
"""

EXPORTS_FILES_TEMPLATE = """
exports_files(glob(["bin/*"]))
"""

def _exec_pkg_config(repo_ctx, flag):
    binary = repo_ctx.which("pkg-config")
    result = repo_ctx.execute([binary, flag] + repo_ctx.attr.packages)
    items = result.stdout.strip().split(" ")
    uniq_items = sorted({key: None for key in items}.keys())
    return uniq_items

def _make_strlist(list):
    return "\"" + "\",\n        \"".join(list) + "\""

def _symlinks(repo_ctx, paths):
    for path in paths:
        if repo_ctx.path(path).exists:
            continue
        repo_ctx.symlink("/" + path, path)

def _pkg_config_repository_impl(repo_ctx):
    includes = _exec_pkg_config(repo_ctx, "--cflags-only-I")
    includes = [item[len("-I/"):] for item in includes]
    _symlinks(repo_ctx, includes)
    data = {
        "name": repo_ctx.attr.name,
        "hdrs": _make_strlist([item + "/**" for item in includes]),
        "copts": _make_strlist(_exec_pkg_config(repo_ctx, "--cflags-only-other")),
        "includes": _make_strlist(includes),
        "linkopts": _make_strlist(_exec_pkg_config(repo_ctx, "--libs-only-l")),
    }
    build_file_data = BUILD_TEMPLATE.format(**data)

    # host_bins
    host_bins = _exec_pkg_config(repo_ctx, "--variable=host_bins")
    if len(host_bins) == 1:
        repo_ctx.symlink(host_bins[0], "bin")
        build_file_data += EXPORTS_FILES_TEMPLATE

    repo_ctx.file("BUILD.bazel", build_file_data)

pkg_config_repository = repository_rule(
    implementation = _pkg_config_repository_impl,
    attrs = {
        "packages": attr.string_list(),
    },
)
```
:::


## å®Ÿéš›ã®ä½¿ç”¨ä¾‹

* [mozc/src/WORKSPACE.bazel](https://github.com/google/mozc/blob/9da52ff96e9ecc33b38612f17a6e9abb60c81bf0/src/WORKSPACE.bazel#L56)
* [mozc/src/unix/ibus/BUILD.bazel](https://github.com/google/mozc/blob/9da52ff96e9ecc33b38612f17a6e9abb60c81bf0/src/unix/ibus/BUILD.bazel#L76)
* [mozc/src/pkg_config_repository.bzl](https://github.com/google/mozc/blob/9da52ff96e9ecc33b38612f17a6e9abb60c81bf0/src/pkg_config_repository.bzl)


# å‹•ä½œã®èª¬æ˜
å‹•ä½œã‚’ç†è§£ã—ã‚„ã™ãã™ã‚‹ãŸã‚ã«ã€ã¾ãšã¯ pkg-config ã‚’ä½¿ã‚ãªã„æ–¹æ³•ã‚’èª¬æ˜ã—ã¾ã™ã€‚ã“ã¾ã‹ã„å†…å®¹ã§ã™ã®ã§å¿…è¦ãªã‘ã‚Œã°é£›ã°ã—ã¦ã‚‚å¤§ä¸ˆå¤«ã§ã™ã€‚

## pkg-config ã‚’ç›´æ¥ä½¿ã‚ãªã„ã§ Bazel ã‹ã‚‰å¤–éƒ¨ãƒ©ã‚¤ãƒ–ãƒ©ãƒªã‚’ä½¿ã†æ–¹æ³•

Bazel ã§ã¯ã€ä½¿ç”¨ã™ã‚‹å¤–éƒ¨ãƒ©ã‚¤ãƒ–ãƒ©ãƒªã®ãƒ˜ãƒƒãƒ€ãƒ¼ãƒ•ã‚¡ã‚¤ãƒ«ã‚„ã‚½ãƒ¼ã‚¹ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ã€ãƒ¬ãƒã‚¸ãƒˆãƒªã€ã¨ã„ã†æ¦‚å¿µã§ç®¡ç†ã—ã¾ã™ã€‚ã“ã‚Œã¯å¤‰æ›´ã•ã‚Œã†ã‚‹ã™ã¹ã¦ã®ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ç®¡ç†ã™ã‚‹ã“ã¨ã§ã€ãƒ“ãƒ«ãƒ‰ã®ä¸€è²«æ€§ã‚’ç¢ºä¿ã™ã‚‹ãŸã‚ã§ã™ã€‚

é–‹ç™ºãƒã‚·ãƒ³ã®ãƒ­ãƒ¼ã‚«ãƒ«ãªç’°å¢ƒã«ã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã•ã‚ŒãŸãƒ©ã‚¤ãƒ–ãƒ©ãƒªã®å ´åˆã€`new_local_repository` ã¨ã„ã† Bazel ã®ãƒ“ãƒ«ãƒ‰ãƒ«ãƒ¼ãƒ«ã‚’ä½¿ç”¨ã—ã¾ã™ã€‚

```py:WORKSPACE.bazel
new_local_repository(
    name = "qt",
    build_file = "BUILD.qt.bazel",
    path = "/usr/include/x86_64-linux-gnu/qt5",
)
```

* new_local_repository ã® name ã«å¾“ã£ã¦ã€`@qt//:` ãŒã“ã®ãƒ¬ãƒã‚¸ãƒˆãƒªã‚’ç¤ºã™åå‰ã«ãªã‚Šã¾ã™ã€‚
* new_local_repository ã® path ãŒ Bazel ã‹ã‚‰ç®¡ç†ã•ã‚Œã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚å…·ä½“çš„ã«ã¯ path ã¸ã®ã‚·ãƒ³ãƒœãƒªãƒƒã‚¯ãƒªãƒ³ã‚¯ãŒ Bazel ã®ä½œæ¥­ç”¨ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªå†… (bazel-src/external/) ã«ä½œæˆã•ã‚Œã¾ã™ã€‚
* new_local_repository ã® build_file ãŒã“ã®ãƒ¬ãƒã‚¸ãƒˆãƒªç”¨ã®ãƒ“ãƒ«ãƒ‰ãƒ•ã‚¡ã‚¤ãƒ«ã§ã™ã€‚

`BUILD.qt.bazel` ã¯æ¬¡ã®ã‚ˆã†ã«æ›¸ãã¾ã™ã€‚

```py:BUILD.qt.bazel
cc_library(
    name = "qt",
    hdrs = glob([
        "QtCore/**",
        "QtGui/**",
        "QtWidgets/**",
    ]),
    includes = [
      ".",
      "QtCore",
      "QtGui",
      "QtWidgets",
    ],
    linkopts = [
        "-lQt5Core",
        "-lQt5Gui",
        "-lQt5Widgets",
    ],
)
```

hdrs ãŠã‚ˆã³ includes ã«æ›¸ã‹ã‚Œã¦ã‚‹ãƒ‘ã‚¹ã¯ new_local_repository.path (ã¤ã¾ã‚Š "/usr/include/x86_64-linux-gnu/qt5") ã‹ã‚‰ã®ç›¸å¯¾ãƒ‘ã‚¹ã§ã™ã€‚
cc_library ã®è©³ç´°ã«ã¤ã„ã¦ã¯[ãƒªãƒ•ã‚¡ãƒ¬ãƒ³ã‚¹](https://bazel.build/reference/be/c-cpp?hl=en#cc_library)ã‚’å‚ç…§ã—ã¦ãã ã•ã„ã€‚


ä¸Šè¨˜ã®ãƒ•ã‚¡ã‚¤ãƒ«ãƒ‘ã‚¹ã‚„ã‚³ãƒ³ãƒ‘ã‚¤ãƒ«ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã¯ã€pkg-config ã‚’äº‹å‰ã«å®Ÿè¡Œã—ã¦å–å¾—ã—ã¦ã„ã¾ã™ã€‚

```sh
 % pkg-config --cflags-only-I Qt5Core Qt5Gui Qt5Widgets
-I/usr/include/x86_64-linux-gnu/qt5/QtWidgets -I/usr/include/x86_64-linux-gnu/qt5 -I/usr/include/x86_64-linux-gnu/qt5/QtGui -I/usr/include/x86_64-linux-gnu/qt5 -I/usr/include/x86_64-linux-gnu/qt5/QtCore -I/usr/include/x86_64-linux-gnu/qt5
```

```sh
 % pkg-config --libs-only-l Qt5Core Qt5Gui Qt5Widgets
-lQt5Widgets -lQt5Gui -lQt5Core
```


## pkg-config ã‚’ Bazel ã‹ã‚‰ä½¿ã†æ–¹æ³•

ä¸Šè¨˜ã® new_local_repository.path ã¨ BUILD.qt.bazel ã®å†…å®¹ã‚’å«ã‚“ã  pkg-config ã‹ã‚‰å–å¾—ã—ã€ãã®å†…å®¹ã‚’åã‚ãŸ Bazel ã®ãƒ¬ãƒã‚¸ãƒˆãƒªã‚’å‹•çš„ã«ä½œæˆã—ã¾ã™ã€‚

Bazel ã®ãƒ¬ãƒã‚¸ãƒˆãƒªã‚’å‹•çš„ã«ä½œæˆã™ã‚‹ã«ã¯ã€[repository_rule](https://bazel.build/rules/repository_rules?hl=en) ã¨ã„ã†ä»•çµ„ã¿ã‚’æ‹¡å¼µã—ã¦ç‹¬è‡ªãƒ«ãƒ¼ãƒ«ã‚’ä½œæˆã—ã¾ã™ã€‚ã“ã®æ–‡æ›¸ã®å…ˆé ­ã® `pkg_config_repository.bzl` ã®ã“ã¨ã§ã™ã€‚

`pkg_config_repository.bzl` ã§ã¯ãŠã‚‚ã«ä»¥ä¸‹ã®ã“ã¨ã‚’ã—ã¦ã„ã¾ã™ã€‚
* ã‚¤ãƒ³ã‚¯ãƒ«ãƒ¼ãƒ‰ãƒ‘ã‚¹ã¸ã®ã‚·ãƒ³ãƒœãƒªãƒƒã‚¯ãƒªãƒ³ã‚¯ã‚’ä½œæˆã™ã‚‹ã€‚
* pkg-config ã‹ã‚‰å¾—ãŸãƒ“ãƒ«ãƒ‰ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã‚’ãƒ“ãƒ«ãƒ‰ãƒ«ãƒ¼ãƒ«ã«åæ˜ ã•ã›ã‚‹ã€‚

### ã‚¤ãƒ³ã‚¯ãƒ«ãƒ¼ãƒ‰ãƒ‘ã‚¹ã¸ã®ã‚·ãƒ³ãƒœãƒªãƒƒã‚¯ãƒªãƒ³ã‚¯ã®ä½œæˆ
pkg-coinfig ã« `--cflags-only-I` ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã‚’ã¤ã‘ã‚‹ã¨ã€ã‚¤ãƒ³ã‚¯ãƒ«ãƒ¼ãƒ‰ãƒ‘ã‚¹ã¸ã®æƒ…å ±ãŒå¾—ã‚‰ã‚Œã¾ã™ã€‚
ãã—ã¦ã€å¾—ã‚‰ã‚ŒãŸãƒ‘ã‚¹ã®ãƒ«ãƒ¼ãƒˆã‹ã‚‰ã®ã‚·ãƒ³ãƒœãƒªãƒƒã‚¯ãƒªãƒ³ã‚¯ã‚’ Bazel ã®å†…éƒ¨ã«ä½œæˆã—ã¾ã™ã€‚

ãŸã¨ãˆã°ã€ã¤ãã® 2 ã¤ã®ãƒ‘ã‚¹ã®å ´åˆã‚’è€ƒãˆã¾ã™ã€‚
```
-I/usr/include/x86_64-linux-gnu/qt5/QtCore
-I/usr/include/x86_64-linux-gnu/qt5/QtGui
```

ã“ã®å ´åˆã¯ã€å†…éƒ¨ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‹ã‚‰ä¸‹è¨˜ã®ã‚³ãƒãƒ³ãƒ‰ã¨åŒæ§˜ã®æ“ä½œã‚’è¡Œã„ã¾ã™ã€‚
```
ln -s /usr/include/x86_64-linux-gnu/qt5/QtCore usr/include/x86_64-linux-gnu/qt5/QtCore
ln -s /usr/include/x86_64-linux-gnu/qt5/QtGui usr/include/x86_64-linux-gnu/qt5/QtGui
```

ãã—ã¦ã€ã‚¤ãƒ³ã‚¯ãƒ«ãƒ¼ãƒ‰ãƒ‘ã‚¹ã‹ã‚‰å…ˆé ­ã® `/` ã‚’é™¤ã„ãŸã€ä¸‹è¨˜ã®ã‚‚ã®ã‚’ Bazel å†…ã§ä½¿ç”¨ã—ã¾ã™ã€‚
```
-Iusr/include/x86_64-linux-gnu/qt5/QtCore
-Iusr/include/x86_64-linux-gnu/qt5/QtGui
```

### pkg-config ã‹ã‚‰ã®ãƒ“ãƒ«ãƒ‰ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã‚’ãƒ“ãƒ«ãƒ‰ãƒ«ãƒ¼ãƒ«ã¸åæ˜ 

pkg-config ã‹ã‚‰å¿…è¦ãªæƒ…å ±ã®ã¿ã‚’å¾—ã‚‰ã‚Œã‚‹ã‚ˆã†ã«ã€ã‚³ãƒãƒ³ãƒ‰ãƒ©ã‚¤ãƒ³ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã‚’æŒ‡å®šã—ã¦ã€Bazel ã®ãƒ«ãƒ¼ãƒ«ã®å€¤ã«åæ˜ ã•ã›ã¾ã™ã€‚

| pkg-config ã®ã‚ªãƒ—ã‚·ãƒ§ãƒ³ | Bazel ã®ãƒ«ãƒ¼ãƒ«ã®å€¤ |
| - | - |
| --cflags-only-I | hdrs, includes |
| --cflags-only-other | copts |
| --libs-only-l | linkopts |


### ãƒ©ã‚¤ãƒ–ãƒ©ãƒªç”¨ãƒã‚¤ãƒŠãƒªã®åæ˜ 
ãƒ©ã‚¤ãƒ–ãƒ©ãƒªã«ã‚ˆã£ã¦ã¯ã€ãƒ„ãƒ¼ãƒ«ç”¨ãƒã‚¤ãƒŠãƒªã‚’å«ã‚“ã§ã„ã‚‹ã‚‚ã®ã‚‚ã‚ã‚Šã¾ã™ã€‚ãŸã¨ãˆã° Qt ã®å ´åˆã€uic ã‚„ rcc ãªã©ã®ãƒã‚¤ãƒŠãƒªãŒã‚ã‚Šã¾ã™ã€‚
ã“ã®ãƒã‚¤ãƒŠãƒªç”¨ã®ãƒ‘ã‚¹ã¯ pkg-config ã« `--variable=host_bins` ã‚’æŒ‡å®šã™ã‚‹ã¨å¾—ã‚‰ã‚Œã¾ã™ã€‚

ã“ã®ãƒ‘ã‚¹ã¸ã®ã‚·ãƒ³ãƒœãƒªãƒƒã‚¯ãƒªãƒ³ã‚¯ã‚’ Bazel å†…éƒ¨ã® bin/ (ã¤ã¾ã‚Š bazel-src/external/qt/bin) ã¨ã—ã¦ä½œæˆã™ã‚‹ã“ã¨ã§ã€`//@qt:bin/uic` ã¨ã—ã¦å‚ç…§ã§ãã‚‹ã‚ˆã†ã«ã—ã¾ã™ã€‚


# æ³¨æ„ç‚¹

* pkg-config ã§å®Ÿéš›ã«å¿…è¦ã«ãªã£ãŸæ©Ÿèƒ½ã®ã¿ã«å¯¾å¿œã—ã¦ã„ã¾ã™ã€‚
* `--cflags-only-I`, `--cflags-only-other`, `--libs-only-l`, `--variable=host_bins` ä»¥å¤–ã®ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã«ã¯å¯¾å¿œã—ã¦ã„ã¾ã›ã‚“ã€‚ 
* `--variable=host_bins` ãŒè¤‡æ•°ã®ãƒ‘ã‚¹ã‚’è¿”ã™å ´åˆã‚„ã€`bin/` ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªãŒä»–ã®ã‚ªãƒ—ã‚·ãƒ§ãƒ³ã¨ç«¶åˆã™ã‚‹å ´åˆã«ã¯å¯¾å¿œã—ã¦ã„ã¾ã›ã‚“ã€‚


# ã¾ã¨ã‚
ãƒ“ãƒ«ãƒ‰ã‚·ã‚¹ãƒ†ãƒ ã® Bazel ã§ pkg-config ã‚³ãƒãƒ³ãƒ‰ã‚’çµ„ã¿è¾¼ã‚€æ–¹æ³•ã‚’ç´¹ä»‹ã—ã¾ã—ãŸã€‚

Bazel ã¯æ¯”è¼ƒçš„å¤§æ›ã‹ã‚Šãªãƒ“ãƒ«ãƒ‰ã‚·ã‚¹ãƒ†ãƒ ã§ã€ã‹ã‚†ã„ã¨ã“ã‚ã«æ‰‹ãŒå±Šãã«ãã„é¢ãŒã‚ã‚‹ã¨æ€ã„ã¾ã™ã€‚
pkg-config ã¨ã®é€£æºã‚‚ãã®ã²ã¨ã¤ãªã®ã§ã™ãŒã€è§£æ±ºæ–¹æ³•ã®å‚è€ƒã«ãªã‚Šã¾ã—ãŸã‚‰ã†ã‚Œã—ã„ã§ã™ã€‚


